/*
 *  This file is part of RocketPlaceholders, licensed under the MIT License.
 *
 *  Copyright (c) Lorenzo0111
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id 'maven-publish'
}

apply from: 'https://cdn.rocketplugins.space/spigot'

group = 'me.Lorenzo0111'
version = '1.9.1-SNAPSHOT'
description = 'RocketPlaceholders'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url = 'http://repo.mvdw-software.be/content/groups/public/'
        allowInsecureProtocol = true
    }
    maven {
        name = 'skullcreator-repo'
        url = 'https://github.com/deanveloper/SkullCreator/raw/mvn-repo/'
    }
}

dependencies {
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly ('be.maximvdw:MVdWPlaceholderAPI:3.1.1-SNAPSHOT') {
        exclude module: 'spigot'
    }
    implementation 'mysql:mysql-connector-java:8.0.25'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation "me.mattstudios.utils:matt-framework-gui:2.0.3.3"
    implementation 'dev.dbassett:skullcreator:3.0.1'
}

shadowJar {
    relocate('com.google.gson','me.lorenzo0111.lib.gson')
    relocate('google.protobuf', 'me.lorenzo0111.lib.protobuf')
    relocate('com.mysql', 'me.lorenzo0111.lib.mysql')
    relocate('com.google.protobuf', 'me.lorenzo0111.lib.google.protobuf')
    relocate('me.mattstudios.mfgui', 'me.lorenzo0111.lib.gui')
    relocate('dev.dbassett.skullcreator', 'me.lorenzo0111.lib.skullcreator')
}

javadoc {
    options.links(
            'https://javadoc.io/static/org.jetbrains/annotations/20.1.0/',
            'https://docs.oracle.com/javase/8/docs/api/',
            'https://papermc.io/javadocs/paper/1.16/'
    )
    source = sourceSets.main.allJava
    include("**/api/*.java")
    include("**/creator/*.java")
    include("**/creator/conditions/*.java")
    include("**/creator/conditions/engine/Requirements.java")
    exclude("**/api/RocketPlaceholdersAPIManager.java")
    destinationDir = new File("javadocs/rocketplaceholders")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = description
            version = project.version

            from components.java

            pom {
                name = 'RocketPlaceholders'
                description = 'A minecraft plugin to add custom placeholders'
                url = 'https://github.com/Lorenzo0111/RocketPlaceholders'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/Lorenzo0111/RocketPlaceholders/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'lorenzo0111'
                        name = 'Lorenzo0111'
                        email = 'lorenzo@rocketplugins.space'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Lorenzo0111/RocketPlaceholders.git'
                    developerConnection = 'scm:git:ssh://github.com/Lorenzo0111/RocketPlaceholders.git'
                    url = 'https://github.com/Lorenzo0111/RocketPlaceholders'
                }
            }

        }
    }

    repositories {
        maven {
            def snapshotUrl = "https://repo.codemc.io/repository/maven-snapshots/"
            def releaseUrl = "https://repo.codemc.io/repository/maven-releases/"

            url = project.version.endsWith("SNAPSHOT") ? snapshotUrl : releaseUrl

            def mavenUsername = System.getenv("NEXUS_USERNAME") ? System.getenv("NEXUS_USERNAME") : null
            def mavenPassword = System.getenv("NEXUS_PASSWORD") ? System.getenv("NEXUS_PASSWORD") : null

            if(mavenUsername != null && mavenPassword != null) {
                credentials {
                    username = mavenUsername
                    password = mavenPassword
                }
            }
        }
    }
}
